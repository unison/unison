#!/usr/bin/env perl
# run-pfregexp -- insert pfregexp entries into Unison

# NOTE: this script should be fixed to run on only on the given sequence list


use warnings;
use strict;

use Unison;
use Unison::run;
use Unison::SQL;
use Getopt::Long qw(:config gnu_getopt);
use Data::Dumper;
use Error qw(:try);
use Base::arrayUtilities qw(acomm);

sub get_regexps($$);
sub get_matching_pseq_ids($@);
sub firstN($@);

my %opts = (
			'debug' => 0,
			'params_id' => undef,
			'pmodelset_id' => undef,
	                'run_id' => undef,
	                'update'    => 0,
			'verbose' => 1,
		   );
GetOptions(\%opts,
		   'debug+',
	           'run_id|r=i',
	           'update',
		   'verbose!',
		  )
  or die("$0: bad usage\n");

if (defined $opts{params_id} || (defined $opts{pmodelset_id})) {
    die("$0: use run_id (-r),  may not specify params_id/pmodelset_id\n");
}

my $u = new Unison();

($opts{params_id},$opts{pmodelset_id}) = (defined $opts{run_id} ? ($u->get_run_params_id_pmodelset_id($opts{run_id})) :  die("$0: run not defined (-r)\n"));

(defined $opts{params_id})
    || die("$0: could not fetch parameters from run_id $opts{run_id}\n");
(defined $opts{pmodelset_id})
    || die("$0: could not fetch modelset from run_id $opts{run_id}\n");

if ($opts{debug}) {
  select STDERR; $|++;
  select STDOUT; $|++;
  print Dumper(\%opts);
}

my %regexps;

# get the models as a hash of pmodel_id => regexp
%regexps = get_regexps($u,$opts{pmodelset_id});
my @pmodel_ids = sort keys %regexps;
printf("%d models selected\n", $#pmodel_ids+1);


my (@pseq_ids) = @ARGV ? map {eval "$_"} @ARGV : map { chomp;$_ } <>;
my $N = $#pseq_ids+1;
my $M = $#pmodel_ids+1;
# Filter the requested pseq_ids for those which actually hit any of the
# regexps. If the number of pseq_ids is large, we can save the time to
# fetch each sequence by doing a server-side scan for pseq_ids which hit
# ANY of the regexps, and using that locally to determine which sequences
# to fetch.
# This is very slow when the number of regular expressions to match is large, 
# so restrict to smaller modelsets
my %hits;
if ($N >= 10000 and $M <= 100) {
  print(STDERR "# using server-side expression matching...\n");
  %hits = map {$_=>1} get_matching_pseq_ids($u,values %regexps);
  my $nhits = scalar(keys %hits);
  printf(STDERR "# matched %d/$N (%5.1f %%) sequences\n",
		 $nhits, $nhits/$N*100);
}

my $insert_sth = $u->prepare('insert into pfregexp (params_id,pseq_id,pmodel_id,start,stop) values (?,?,?,?,?)');

PSEQ_ID:
for(my $i=0; $i<=$#pseq_ids; $i++) {
  my $pseq_id = $pseq_ids[$i];
  my $leader = sprintf("\rUnison:%-8d %d/%d (%5.1f%%)", $pseq_id, $i+1, $N, ($i+1)/$N*100);
  print(STDERR $leader);

  my $ran_on = $u->get_run_timestamp($pseq_id,$opts{run_id});
  if (defined $ran_on and not $opts{update}) {
    print(STDERR ": run with these options on $ran_on; skipping\n");
    next PSEQ_ID;
  }

  if (not %hits or exists $hits{$pseq_id}) {
	my @mbe;
	my $seq = $u->get_sequence_by_pseq_id($pseq_id);
	if (not defined $seq) {
	  warn("couldn't retrieve Unison:$pseq_id; skipping\n");
	  next PSEQ_ID;
	}
	foreach my $pmodel_id (@pmodel_ids) {
	  my $re = qr/$regexps{$pmodel_id}/;
	  while ($seq =~ m/$re/g) {
		my $e = pos($seq);
		push(@mbe,[$pmodel_id,$e-length($&)+1,$e])
	  }
	}
	printf(STDERR "%4d features...", $#mbe+1);
	foreach my $mbe (@mbe) {
	  try {
	    if($opts{update}) {
	      my $dsql = 'delete from pfregexp where pseq_id=? and params_id=? and pmodel_id=?';
	      my $dsth = $u->prepare($dsql);
	      $dsth->execute($pseq_id,$opts{params_id},$mbe->[0]);
	    }
	    $insert_sth->execute($opts{params_id},$pseq_id,@$mbe);
	  } catch Unison::Exception::DBIError with {
		my $e = $_[0];
		if ("$e" !~ m/duplicate key/) {
		  die("$leader\n$e\n");
		};
		# else, ignore it
	  };
	}
	my $z = $u->upd_run_history($pseq_id,$opts{run_id});
      }
}
print(STDERR "\n");


sub get_regexps($$) {
  # return hash of pmodel_id => regexp for pmodelset.
  my ($u,$m) = @_;
  my $sql = new Unison::SQL;
  $sql->columns('M.pmodel_id,M.regexp')
      ->table('pmregexp M');
  if (defined $m) {
	$sql->join('pmsm_pmregexp S on M.pmodel_id=S.pmodel_id')
	    ->where('S.pmodelset_id =' . $m );
  }
  $sql->order('M.pmodel_id');
  return( map {$_->[0]=>$_->[1]} @{$u->selectall_arrayref("$sql")} );
}

sub get_matching_pseq_ids($@) {
  my ($u,@re) = @_;
  my $sql = 'select distinct pseq_id from pseq where ('
	. join(' or ', map {"seq~'$_'"} @re)
	. ') order by pseq_id';
  return( map {@$_} @{$u->selectall_arrayref($sql)} );
}

sub firstN($@) {
  my $N = shift;
  return '' unless (defined @_);
  return join(',', $#_<$N ? @_ : (@_[0..$N],'...') );
}
