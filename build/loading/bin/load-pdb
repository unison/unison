#!/usr/bin/env perl

$^W++;
use strict;
use lib ('./perl5','/home/rkh/unison/perl5');
use Unison;

$|++; select(STDERR); $|++; select(STDOUT);


my $udb = new Unison;
my $origin = 'pdb';
my $porigin_id = $udb->si_porigin_id($origin);
printf(STDERR "# loading sequences into $origin (porigin_id=$porigin_id)\n");


my %ONECODE =
  ('ALA' => 'A', 'ASX' => 'B', 'CYS' => 'C', 'ASP' => 'D',
   'GLU' => 'E', 'PHE' => 'F', 'GLY' => 'G', 'HIS' => 'H',
   'ILE' => 'I', 'LYS' => 'K', 'LEU' => 'L', 'MET' => 'M',
   'ASN' => 'N', 'PRO' => 'P', 'GLN' => 'Q', 'ARG' => 'R',
   'SER' => 'S', 'THR' => 'T', 'VAL' => 'V', 'TRP' => 'W',
   'XAA' => 'X', 'TYR' => 'Y', 'GLX' => 'Z', 'TER' => '*',
   'SEL' => 'U'
  );




while (my $fn = shift)
  {
  my ($sr,$len,$desc) = read1($fn);
  my $id = $fn;
  $id =~ s%.*/%%; $id =~ s/^pdb//; $id =~ s/\.(ent|pdb).*//;
  printf(STDERR "$id: %d chains={%s}; %s\n",
		 scalar keys %$sr, join(',',keys %$sr), $desc);
  for my $c (sort keys %$sr)
	{
	if ( $len->{$c} != length($sr->{$c}) )
	  { warn("! $id$c: sequence doesn't match provided sequence length\n"); }
	if ( length($sr->{$c}) < 20 )
	  { warn("! $id$c: discarding short sequence (",length($sr->{$c})," AA)\n"); next; }
	my $dc = $c ne ' ' ? $c : '';
	add1($udb,$porigin_id,$sr->{$c},"$id$dc",$desc);
	}
  }



sub read1
  {
  my $fn = shift;
  my $pdb = `gzip -cdq $fn`;
  my (@desc) = map {s/\s+\d\w\w\w   \d//;$_} $pdb=~m/^TITLE\s+(?:\d\s)?(.+)/mg;
  my $desc = join(' ',@desc);
  $desc =~ s/\s+/ /g; $desc =~ s/^\s//; $desc =~ s/\s$//;
  my %sr;
  my %len;
  while( $pdb =~ m/^SEQRES \s+ \d+ \s+ (.) \s+ (\d+) \s+ ((?:\s\w{3})+)/gmsx )
	{
	$len{$1} = $2;
	$sr{$1} = '' unless exists $sr{$1};
	$sr{$1} .= "$3\n";
	}

  for my $c (sort keys %sr)
	{
	if ($sr{$c} =~ m/\w{3} \w{3}/)
	  {
	  $sr{$c} =~ s/\s+//g;
	  $sr{$c} = xl($sr{$c});
	  }
	else
	  { delete $sr{$c}; }
	}

  return (\%sr,\%len,$desc);
  }


sub xl
  {
  my $s = shift;
  $s =~ s/(...)/exists $ONECODE{$1} ? $ONECODE{$1} : 'X'/ge;
  return $s;
  }

sub add1
  {
  my ($udb,$ori,$seq,$id,$desc) = @_;
  my $pseq_id = $udb->si_pseq_id($seq);
  if (not defined $pseq_id)
	{ warn("sequence add for $id failed"); return; }
  $udb->add_palias($pseq_id,$ori,$id,$desc);
  }
