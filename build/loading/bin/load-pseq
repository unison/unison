#!/usr/bin/env perl

my $usage = <<'EOU';
#-------------------------------------------------------------------------------
# NAME: load-pseq
# PURPOSE: load sequence files into Unison
# USAGE: load-pseq ARGUMENTS OPTIONS <seq files ...>
# ARGUMENTS (required):
#   --origin|-o <origin>: name of origin
#   --format|-f <format>: name of input file format (any format allowed by Bio::SeqIO)
# OPTIONS:
#   --start-after|--startafter <acc>: start parsing input file after this acc
#   --incl-subex|--inclsubex: flag to include suboptimal gene predictions
#   --verbose|-v: flag to output more processing information
#
# $Id: load-pseq,v 1.36 2007/06/20 22:06:41 mukhyala Exp $
#------------------------------------------------------------------------------

EOU

use warnings;
use strict;

use FindBin;
use lib "$FindBin::RealBin/../../perl5", "$FindBin::RealBin/../../perl5-ext";

use Unison;
use Unison::Exceptions;
use Unison::Utilities::misc qw(clean_sequence sequence_md5);

use Benchmark;
use Bio::SeqIO;
use Getopt::Long qw(:config gnu_getopt);
use Module::Load;


sub tally ($%);
sub process_stream ($$);
sub process_seq ($$$);


# acceptable file formats (subset of Bioperl)
my @af = qw(embl fasta fastq genbank locuslink phd pir swiss);
my %af = map { $_=>1 } @af;


my %opts = (
			deprecate => 0,
			origin => undef,
			format => undef,
			'verbose' => 0,
			tax_id => undef,
			'allow-anonymous-sequences' => 0,
			'infer-tax-id' => 0,
			'version' => undef,
			'dry-run' => 0,
			'create-origin' => 0
);
GetOptions( \%opts,
			'create-origin+',
			'origin|o=s',
			'format|f=s',
			'verbose|v+',
			'tax_id|t=i',
			'allow-anonymous-sequences+',
			'infer-tax-id+',
			'version|V=s',
			'dry-run|n+',
			'deprecate+',
		  )
  || die("$0: bad usage:\n$usage\n");


(defined $opts{origin})
  || die("$0: --origin (-o) is mandatory\n$usage\n");
(defined $opts{format} && $af{lc($opts{format})})
  || die("$0: --format (-f) is mandatory and must be one of (",
		 join(',',sort {$a cmp $b} keys %af),")\n$usage\n");


my $u = new Unison();

my %origin_to_origin_id;
%origin_to_origin_id = map {$_->[0] => $_->[1]}
  @{$u->selectall_arrayref( 'select origin,origin_id from origin' )};
$opts{origin_id} = $origin_to_origin_id{$opts{origin}};
if (not defined $opts{origin_id}) {
  if ($opts{'create-origin'}) {
	my $sth = $u->prepare('insert into origin(origin) values (?)');
	$sth->execute($opts{origin});
	$sth->finish();

	%origin_to_origin_id = map {$_->[0] => $_->[1]}
	  @{$u->selectall_arrayref( 'select origin,origin_id from origin' )};
	$opts{origin_id} = $origin_to_origin_id{$opts{origin}};
  } else {
	die("$0: origin $opts{origin} doesn't exist; consider --create-origin\n");
  }
}

my %latin_to_tax_id = map  {$_->[0] => $_->[1]}
  @{$u->selectall_arrayref( 'select lower(latin),tax_id from tax.spspec' )};
my %gs_to_tax_id = map  {$_->[0] => $_->[1]}
  @{$u->selectall_arrayref( 'select upper(gs),tax_id from tax.spspec' )};
my %known_tax_ids = map {$_ => 1} values %latin_to_tax_id;

(defined $opts{tax_id} and $opts{'infer-tax-id'})
  && die("$0: you may not specify a tax_id and request infer-tax-id\n");

(defined $opts{tax_id} and not exists $known_tax_ids{$opts{tax_id}})
  && die("$0: you specified an invalid tax_id\n");

($opts{origin} =~ m/^RefSeq/ and $opts{format} !~ m/genbank/i)
  && die("$0: $opts{origin} sequences should be loaded from genbank format files\n");

($opts{origin} =~ m/^UniProt/ and $opts{format} !~ m/swiss/i)
  && die("$0: $opts{origin} sequences should be loaded from swiss format files\n");



my %warnings;					  # hash of warnings to prevent duplicates
my @tally_bins = qw(nseen nadded nskipped nfailed naliases);
my %totals = map { $_=>0 } @tally_bins;
my $tts = new Benchmark;

# pannotation_ids_to_deprecate -- Initially, a hash of all palias ids in this
# origin.  As aliases are loaded, we delete from this hash.  When we're
# done loading sequences, any remaining palias ids refer to accessions
# that are no longer in the source database.
my %pannotation_ids_to_deprecate;
if ($opts{deprecate}) {
  %pannotation_ids_to_deprecate = map {$_->[0]=>1}
	@{$u->selectall_arrayref("select pannotation_id from palias where origin_id=origin_id('$opts{origin}')")};
}



$u->begin_work();
foreach my $fn ( @ARGV ) {
  # Use IO::Zlib or IO::File, depending on whether the suffix suggests compression
  # STDIN is presumed to be uncompressed
  my $fh;
  if ($fn =~ m/\.gz$/) {
	##The code below using IO::Zlib is broken because IO::Zlib ignores $/,
	##which is required for parsing multi-line records.  From the IO::Zlib
	##perldoc: 
	##   getline
    ##     Return the next line from the file, or undef on end of string.  Can
    ##     safely be called in an array context.  Currently ignores $/
    ##     ($INPUT_RECORD_SEPARATOR or $RS when English is in use) and treats
    ##     lines as delimited by "\n".
	## In the interime, I'll open an IO::Pipe.
	## -- 2008-05-22 Reece Hart <reece@harts.net>
	# $fh = new IO::Zlib;
	# $fh->open($fn,'rb')
	#   || die("$fn: $!\n");

	load IO::Pipe;
	$fh = new IO::Pipe;
	$fh->reader('gzip', '-cdq', $fn)
	   || die("gzip -cd $fn: $!\n");
  } else {
	load IO::File;
	$fh = new IO::File;
	$fh->open($fn)
	  || die("$fn: $!\n");
  }

  my $stream = Bio::SeqIO->new(-fh => $fh, 
							   -format => $opts{format});
  my $tis = new Benchmark;
  my $stats = process_stream($u,$stream);
  my $tie = new Benchmark;
  my $tidelta = @{timediff($tie, $tis)}[0];
  print(STDERR tally($fn,$tidelta,%$stats), "\n");

  $totals{$_} += $stats->{$_} for @tally_bins;
  $fh->close();
}

my $tte = new Benchmark;
my $ttdelta = @{timediff($tte, $tts)}[0];
print(STDERR tally('totals',$ttdelta,%totals), "\n");


if ($totals{nfailed} > 0) {
  $u->rollback();
  die("! WARNING: Some sequence and/or aliases failed; origin.last_updated NOT tickled\n");
}


if ($totals{nadded} > 0 and not $opts{'dry-run'}) {
  my $n_per_line = 8;

  if ($opts{deprecate}) {
	my (@pannotation_ids_to_deprecate) = sort {$a<=>$b} keys %pannotation_ids_to_deprecate;
	printf("# Deprecating %d pannotation_ids\n",$#pannotation_ids_to_deprecate+1);
	if ($opts{verbose}>1) {
	  for(my $i=0; $i<=$#pannotation_ids_to_deprecate; $i += $n_per_line) {
		my $top = $i+$n_per_line-1;
		$top = $#pannotation_ids_to_deprecate if $top>$#pannotation_ids_to_deprecate;
		print(join("\t",'',@pannotation_ids_to_deprecate[$i..$top]),"\n");
	  }
	}
	# Note: we check is_current again because nearly all aliases in the hash
	# will have been deprecated by assigning the alias to a new pseq_id.
	# The only sequences we care about are those which aren't in the source
	# database any longer, i.e., have is_current=TRUE still.
	my $sth = $u->prepare('update pseq_pannotation set is_current=false where pannotation_id=? and is_current=TRUE');
	$sth->execute($_) foreach (@pannotation_ids_to_deprecate);
  }

  print( "update origin set last_updated='",
		 $u->origin_last_updated($opts{origin_id},1),
		 "' where origin_id=$opts{origin_id}\n");
  print( "update origin set version='",
       $u->origin_version($opts{origin_id},$opts{version}),
       "' where origin_id=$opts{origin_id}\n") if(defined $opts{version});
  $u->commit();
} else {
  $u->rollback();
  warn("! WARNING: No sequences loaded; origin.last_updated NOT tickled\n");
}
exit(0);





###########################################################################
## INTERNALS

## NAME: process_stream
## PURPOSE: parse Bio::SeqIO stream and load sequences into Unison
## ARGUMENTS: Bio::SeqIO object, option hashref:
##'origin' => name of origin (REQUIRED)
##  'verbose' => boolean for whether to output more information
## RETURNS: hash with process info keys: nseen, nskipped, nadded, nfailed
sub process_stream ($$) {
  my ($u,$in) = @_;
  my %stats = map {$_=>0} @tally_bins;
  while ( my $bs = $in->next_seq() ) {
	try {
	  process_seq($u,$bs,\%stats);
	} catch Unison::Exception with {
	  warn($_[0]);
	};
  }
  return(\%stats);
}


## NAME: process_seq
## PURPOSE: parse Bio::Seq object and load seq and alia into Unison
## ARGUMENTS: Bio::Seq object, option hashref, process info hashref (keys:
##  nseen, nskipped, nadded), hashref of allowable tax_ids
## NOTES: sequences loaded into the "refseq" origin are loaded with sequence
##		version numbers; all others origins ignore sequence version, if any.
## RETURNS: nada
sub process_seq ($$$) {
  my ($u,$bs,$stats) = @_;

  my %md5;
  my $pseq_id;
  my $frommd5;
  my @anno = ();				 # @ of [origin_id, alias, descr]

  my $seq = clean_sequence( $bs->seq() );
  my $md5 = sequence_md5($seq);
  my $id = $bs->display_id();
  my $descr = $bs->desc();
  my $tax_id = ( (defined $bs->species and defined $bs->species->ncbi_taxid())
				 ? $bs->species->ncbi_taxid()
				 : $opts{tax_id} );

  $stats->{nseen}++;

  # attend to some of the vagaries of human curation and folly...
  $descr = '' unless defined $descr;
  $descr =~ s/\02//g;						# remove ^B (some Proteome)
  $descr =~ s/\s{2,}/ /gm;					# including \s\n\s
  $descr =~ s/(\S)\n(\S)/$1$2/;				# newline amid non-whitespace


  # split ctrl-A delimited entries into explict entries for this sequence
  # this is effectively a no-op for single-entry deflines.  It's necessary to
  # deal with NCBI fasta files which collect non-redundant annotations into
  # one defline.  Also try to infer the tax_id from the descr
  foreach my $ad ( split(/\01/, "$id $descr") ) {
	my ($a,$d) = $ad =~ m/^(\S+)\s+(.*)/g;
	(defined $a and defined $d)
	  || die("couldn't parse alias and/or descr from '$ad'\n");
	push(@anno, [$opts{origin},$a,$d,$tax_id]);
  }


  # add tax_ids and origin-specific processing of @anno
  my @secondary_anno = ();
  for ( my $i=0; $i<=$#anno; $i++ ) {
	my ($o,$a,$d,$t) = @{$anno[$i]};

	# infer tax from descr [<latin>] at the end of an annotation.  I don't
	# enable this by default because I'm concerned about descriptions like
	# 'ortholog of hsp70 [homo sapiens]' for a non- human sequence.
	if ( not defined $t
		 and $opts{'infer-tax-id'}
		 and my ($latin) = $d =~ m/\[([^\]]+?)\]\W*$/ ) {
	  $latin = lc($latin);
	  if (defined $latin_to_tax_id{$latin}) {
		$t = $latin_to_tax_id{$latin};
		if (defined $tax_id and $t != $tax_id) {
		  warn("! $id ($descr): changed tax_id from $tax_id to $t ($latin)\n");
		}
	  } else {
		$warnings{'unknown latin species'}{$latin}++
		  || warn("! no species found for '$latin', first seen in $a ($d)\n");
	  }
	}

	if ( $o =~ m/^RefSeq/i ) {
	  if ($opts{format} =~ m/genbank/i) {
	    push( @secondary_anno, [ "$o unversioned", $a, $d, $t ] ); 	    #unversioned alias
	    push( @secondary_anno, [ "$o gi", $bs->primary_id(), $d, $t ] );
	    if (not defined $bs->version()) {
	      die("! Unison:$pseq_id couldn't find version for $id ($a: $d)\n");
	    }
	    $a .= '.' . $bs->version(); 	    #versioned alias
	  } elsif ( $a =~ m/^gi\|(\d+)/ ) {
		push( @secondary_anno, [ "$o gi", $1, $d, $t ] );
	  }
	  $a =~ s/^.*ref\|([^\|]+).*/$1/;		# refseq alias only
	  (defined $a)
		|| die("! Unison:$pseq_id ($id) doesn't contain a RefSeq accession!\n");
	} elsif ( $o =~ m/^(?:pataa)/i ) {
	  # the accession mess in pataa deflines isn't necessarily unique.  E.g.,
	  # Unison:1795309, len=11, 2 annotations
	  #    + pataa:gi|998068|gb|AAA76369.1| Sequence 32 from patent US 5451499 [NULL]
	  #    + pataa:gi|998068|gb|AAA76369.1| Sequence 13 from patent US 5451499 [NULL]
	  # So, we'll uniquify it with meaningful text from the descr
	  if ($d =~ m/^Sequence (\d+) from Patent (.+)/) {
		my $PSn = "$2#$1";
		$PSn =~ s/\s+//g;
		$a .= "|$PSn";
	  }

	} elsif ($o =~ m/^Uniprot|Swiss-Prot/i) {
	  if (not defined $t) {
		my ($gs) = $a =~ m/_(\w+)/;
		if (defined $gs) {
		  my $newt = $gs_to_tax_id{uc($gs)};
		}
	  }
	  push(@secondary_anno, [$o,$bs->accession(),$d,$t] );

	} elsif ($o =~ m/^CCDS/i) {
	  $d = $a;
	  ($a) = $d =~ m/^([^|]+)/;

	} elsif ($o =~ m/GenenGenes/i) {
	  $d =~ s/\[(?:min|full)\]\s+//;
	  $d =~ s/\# converted.+//;

	} elsif ($o =~ m/^TNF6F/i) {
	  $d =~ s%/coords.+trans=[FR]\d %%;

	} elsif ($o =~ m/^geneseq$/i) {
	  my ($os) = $d =~ m/\[OS: ([^\[]+)\]/;
	  my $latin = lc($os);
	  if (defined $latin_to_tax_id{$latin}) {
		$t = $latin_to_tax_id{$latin};
	  } else {
		$warnings{'unknown latin species'}{$latin}++
		  || warn("! no species found for '$os', first seen in $a ($d)\n");
	  }
	} elsif ($o =~ m/^string$/i) {
	  # infer tax_id from STRING alias irrespective of infer-tax-id
	  if ( not ( ($t) = $a =~ m/^(\d+)\./ ) ) {
		# STRING aliases always have the form <tax_id>.<text>
		die("Improperly formatted alias `$a' for STRING\n");
	  }
	}

	$anno[$i] = [$o,$a,$d,$t];
  }

  if ($#anno == -1 and not $opts{'allow-anonymous-sequences'}) {
	die("Unison:$pseq_id has no primary annotations and allow-anonymous-sequences is not set\n",
		"id=$id; descr=$descr\n")
  }

  push(@anno, @secondary_anno);


  # select/insert sequence to get a pseq_id or get it by md5 hash from
  # identical sequences already inserted in this session
  if (%md5 and defined $md5{ $md5 }) {
    $frommd5 = '*';
    $pseq_id = $md5{ $md5 };
  } else {
	$frommd5 = '';
	try {
	  $pseq_id = $opts{'dry-run'} ? 0 : $u->pseq_si_pseq_id( $seq );
	  $md5{ $md5 } = $pseq_id;
	  $stats->{nadded}++;
	} catch Unison::Exception with {
	  $stats->{nfailed}++;
	  warn("! Failed to load sequence $id ($descr)\n", $_[0]);
	  return;
	};
  }
  printf(STDERR "* Unison:$pseq_id$frommd5, len=%d, %d annotations\n",
		 length($seq), $#anno+1) if $opts{verbose};


  # load [o,a,d,t] triplets for this pseq_id
  foreach my $anno (@anno)  {
	my ($o,$a,$d,$t) = @$anno;

	if ($a =~ m/\|/ and not $warnings{alias_pipe}++) {
	  warn("! You're loading aliases with `|' (e.g., `$a')\n");
	}

	my $o_id = $origin_to_origin_id{$o};
	(defined $o_id)
	  || die("$0: couln't find origin $o\n");

	# Sequences are tagged with the tax_id only if the tax_id is found in
	# known_tax_ids.  This is necessary to handle discrepancies.
	if (defined $t and not exists $known_tax_ids{$t}) {
	  $warnings{'missing tax_id'}{$t}++
		|| warn("! tax_id=$t not defined in Unison\n");
	  undef $t;
	}

	try {
	  my ($pannotation_id) = $u->assign_alias($pseq_id,$o_id,$a,$d,$t) unless $opts{'dry-run'};
	  delete $pannotation_ids_to_deprecate{$pannotation_id};
	} catch Unison::Exception with {
	  warn("! Failed to load alias $a for Unison:$pseq_id\n",$_[0]);
	};
	printf(STDERR "   + $o:$a %s [%s]\n",$d||'NULL',$t||'NULL') if $opts{verbose}>1;
	$stats->{naliases}++;
  }

  return;
}


sub tally ($%) {
  my ($src,$delta,%stats) = @_;
  sprintf('%-30s %7d seen, %7d added, %7d skipped, %7d failed, %7d aliases, %8ds, %5.1f seen/s',
		  (length($src)>29 ? '...'.substr($src,-26) : $src).':',
		  @stats{qw(nseen nadded nskipped nfailed naliases)},
		  $delta,
		  ($stats{nseen} / ($delta+0.00001))
		 );
}

