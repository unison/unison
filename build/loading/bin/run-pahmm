#!/usr/bin/env perl
# $Id: run-pahmm,v 1.14 2005/05/11 21:53:41 rkh Exp $
# run-pahmm -- run hmmer on specified sequences

# TODO:
# - currently fetches 1, runs 1, loads 1; better to run in a pipe, or in
# batches of sequences if that's problematic. ldhmmer will particularly
# like this

use warnings;
use strict;
use Bio::PrimarySeq;
use Bio::SearchIO;
use Bio::SeqIO;
use Data::Dumper;
use Error qw(:try);
use File::Temp qw(tempfile tempdir);
use Getopt::Long qw(:config gnu_getopt);
use IO::File;
use IO::Pipe;
use Unison::Exceptions;
use Unison::SQL;
use Unison;


sub preload_pmodel_id_cache($$);
sub fetch_sequences ($$@);
sub get_pmodel_id ($);
sub run_and_load ($$);


my %opts = 
  (
   force => 0,
   params_id => undef,
   pmodelset_id => undef,
   verbose => 0,
   'seqs-per-run' => 50
  );
GetOptions(\%opts,
		   'force|f+',
		   'params_id|p=i',
		   'pmodelset_id|M=i@',
		   'verbose|v+',
		   'seqs-per-run|N=i',
		  ) || die("$0: you got usage issues, short stack\n");

my $temproot = "/tmp/run-pahmm-$$";
my $seqfn = "$temproot.fa";

my $u = new Unison( dbname=>'csb-dev', username=>'loader' );

(defined $opts{params_id})
  || die("$0: parameters not defined (-p)\n");

$opts{model_porigin_id} = 
	$u->selectrow_array(<<EOSQL);
SELECT model_porigin_id FROM params
WHERE params_id=$opts{params_id}
EOSQL
(defined $opts{model_porigin_id})
  || die("$0: Big Problem: model_porigin_id not defined for params_id=$opts{params_id}\n");

$opts{modelfile} = 
	$u->selectrow_array(<<EOSQL);
SELECT data_url FROM porigin
WHERE porigin_id=$opts{model_porigin_id}
EOSQL
(defined $opts{modelfile})
  || die("$0: data_url is NULL for porigin_id=$opts{model_porigin_id}\n");
(-e $opts{modelfile})
  || die("$0: $opts{modelfile} doesn't exist\n");

my $nmodels = preload_pmodel_id_cache($u,$opts{model_porigin_id});

my $clo = $u->run_commandline_by_params_id($opts{params_id});
my @cl = (split(' ',$clo), $opts{modelfile}, $seqfn);


printf(STDERR "# loading params '%s' (params_id=%d)\n",
	   $u->get_params_name_by_params_id($opts{params_id}),
	   $opts{params_id});
printf(STDERR "# %d models from porigin '%s' (porigin_id=%d)\n",
	   $nmodels,
	   $u->get_porigin_name_by_porigin_id($opts{model_porigin_id}),
	   $opts{model_porigin_id});
printf(STDERR "# models in file `%s'\n", $opts{modelfile});
printf(STDERR "# commandline: %s\n", join(' ',@cl));


my @fields = qw(params_id pseq_id start stop pmodel_id mstart mstop gaps qgaps tgaps score eval);
my $pahmm_sth = $u->prepare_cached(sprintf("insert into pahmm (%s) values (%s)",
										   join(',',@fields), join(',',map {'?'} @fields)));


my (@pseq_ids) = @ARGV ? map {eval "$_"} @ARGV : map { chomp;$_ } <>;
my $N = $#pseq_ids+1;
my $n = 0;
my $totseqs = 0;
my $tothits = 0;
my $tothsps = 0;
my $toterrs = 0;

while( @pseq_ids ) {
  my (@ids) = splice(@pseq_ids,0,$opts{'seqs-per-run'});

  my $nseq = fetch_sequences($u,$seqfn,@ids);
  next if ($nseq == 0);

  print(STDERR "fetched $nseq sequences; running...\n");

  my ($seqs,$hits,$hsps,$errs) = run_and_load($u,$seqfn);
  $totseqs += $seqs;
  $tothits += $hits;
  $tothsps += $hsps;
  $toterrs += $errs;

  my $Ncomp = $N - ($#pseq_ids+1);
  printf(STDERR "run-pahmm: $Ncomp/$N (%5.1f%% complete); $totseqs seqs; $tothits hits; $tothsps HSPs; $toterrs errors\n",
		 $Ncomp / $N * 100);
}


sub fetch_sequences ($$@) {
  # fetch sequences to temp file
  my $u = shift;
  my $seqfn = shift;
  my @pseq_ids = @_;
  my $nseq = 0;

  my $so = new Bio::SeqIO( -format => 'fasta',
						   -file => ">$seqfn" )
	|| die("! couldn't open $seqfn for writing\n");

  foreach my $pseq_id (@pseq_ids) {
	if ($u->already_ran($pseq_id,$opts{params_id},$opts{model_porigin_id},$opts{pmodelset_id})) {
	  warn("Unison:$pseq_id already run with these parameters\n");
	  next;
	}
	my $seq = $u->get_sequence_by_pseq_id( $pseq_id );
	if (not defined $seq) {
	  warn("\n! couldn't find sequence for pseq_id=$pseq_id\n");
	  next;
	}

	$so->write_seq( new Bio::PrimarySeq( -seq => $seq,
										 -id => "Unison:$pseq_id" ) );
	$nseq++;
  }
  $so->close();
  return $nseq;
}


sub run_and_load ($$) {
  my $u = shift;
  my $seqfn = shift;
  my $totseqs = 0;
  my $tothits = 0;
  my $tothsps = 0;
  my $toterrs = 0;

  my $hmmerpipe = new IO::Pipe;
  $hmmerpipe->reader( @cl )
	|| die("couldn't do @cl\n");
  my $in = new Bio::SearchIO(-format => 'hmmer',
							 -fh => $hmmerpipe);

  while( my $result = $in->next_result ) {	# "result" is a query sequence
	$totseqs++;
	my ($pseq_id) = $result->query_name() =~ m/Unison:(\d+)/;
	(defined $pseq_id)
	  || die("couldn't parse pseq_id from hmmer results\n");
	my $nhits = 0;
	my $nhsps = 0;

	try {
	  $u->begin_work();
	  while( my $hit = $result->next_hit ) {
		$nhits++;
		my $acc = $hit->name();
		my $pmodel_id = get_pmodel_id($acc);
		if (not defined $pmodel_id) {
		  die("\n! couldn't look up a pmodel_id for $acc in origin $opts{model_porigin_id}!\n");
		}

		while( my $hsp = $hit->next_hsp ) {
		  $nhsps++;
		  my @data = ($opts{params_id}, 
					  $pseq_id, $hsp->start('query'), $hsp->end('query'),
					  $pmodel_id, $hsp->start('hit'), $hsp->end('hit'),
					  $hsp->gaps('total'), $hsp->gaps('query'), $hsp->gaps('hit'),
					  int($hsp->score()), $hsp->evalue());
		  $pahmm_sth->execute(@data);
		}
	  }
	  my $z = $u->upd_run_histories($pseq_id,$opts{params_id},$opts{model_porigin_id},$opts{pmodelset_id});
	  $u->commit();
	  printf(STDERR "Unison:$pseq_id: $nhits hits, $nhsps HSPs; committed $z\n");
	  $tothits += $nhits;
	  $tothsps += $nhsps;
	} catch Unison::Exception with {
	  my $e = $_[0];
	  if ("$e" =~ m/duplicate key/ or "$e" =~ m/(?:over|under)flow/) {
		warn("\n$e\nUnison:$pseq_id load failed; rolling back.\n",);
		$u->rollback();
		$toterrs++;
	  } else {
		die("$e");
	  }
	};
  }

  $hmmerpipe->close();
  return( $totseqs, $tothits, $tothsps, $toterrs );
}




my %pmodel_id_cache;

sub preload_pmodel_id_cache($$) {
  my $u = shift;
  my $porigin_id = shift;
  my $ar = $u->selectall_arrayref('select acc,pmodel_id from pmhmm where porigin_id=?',
								 undef, $porigin_id);
  %pmodel_id_cache = map {$_->[0] => $_->[1]} @$ar;
  return $#$ar+1;
}

sub get_pmodel_id ($) {
  my $acc = shift;
  if (not exists $pmodel_id_cache{$acc}) {
	my $sth = $u->prepare_cached('select pmodel_id from pmhmm where acc=? and porigin_id=? order by added desc');
	$sth->execute($acc,$opts{model_porigin_id});
	my $ids = $sth->fetchall_arrayref();
	return undef unless defined $ids;
	die("\n! found ",$#$ids+1," pmodel_ids for model acc $acc\n") if ($#$ids>0);
	$pmodel_id_cache{$acc} = $ids->[0]->[0];
  }
  return $pmodel_id_cache{$acc};
}
