#!/usr/bin/env perl
# $Id: run-pahmm,v 1.28 2006/07/21 16:22:49 mukhyala Exp $
# run-pahmm -- run hmmer on specified sequences

# TODO:
# - currently fetches 1, runs 1, loads 1; better to run in a pipe, or in
# batches of sequences if that's problematic. ldhmmer will particularly
# like this
#
# - nhits is misleading.  I /thought/ that this was the number of distinct
# models, just as is shown at the top of the HMMer output.  It's not --
# hits are only grouped if they're contiguous in sequence.  For example,
# Unison:1021234 with Pfam_ls 18.0 gives these hsps:
#    hit:1; hsp:1; [ 42,100] PF00047.14      0.001
#    hit:2; hsp:2; [136,169] PF00057.8         5.2
#    hit:3; hsp:3; [138,199] PF00047.14        1.5
#    hit:3; hsp:4; [239,299] PF00047.14       0.86
#    hit:4; hsp:5; [325,420] PF07686.4       0.032
#    hit:5; hsp:6; [339,399] PF00047.14    0.00056
# The while( next_* ) loops below give 5 (!) hits for 6 HSPs, even though
# there are only 3 distinct models.  The HMMer output summary shows only 3
# models of course.
# In short: The model grouping at the top of HMMer output is NOT the same
# as the "hit" grouping returned by the bioperl parser.


use warnings;
use strict;
use Bio::PrimarySeq;
use Bio::SearchIO;
use Bio::SeqIO;
use Data::Dumper;
use Error qw(:try);
use File::Temp qw(tempfile tempdir);
use Getopt::Long qw(:config gnu_getopt);
use IO::File;
use IO::Pipe;
use Unison::Exceptions;
use Unison::SQL;
use Unison;


sub preload_pmodel_id_cache($$);
sub fetch_sequences ($$@);
sub get_pmodel_id ($);
sub run_and_load ($$);

my $min_double_prec = 1e-307;				# eval underflow if less

my %opts = 
  (
   force => 0,
   modelfile => undef,
   model_origin_id => undef,
   params_id => undef,
   pmodelset_id => undef,
   modelfile => undef,
   model_origin_id => undef,
   verbose => 0,
   'seqs-per-run' => 50,
   'max-length' => 10000,					# works, but probably too low
  );
GetOptions(\%opts,
		   'force|f+',
		   'modelfile=s',
		   'params_id|p=i',
		   'pmodelset_id|M=i@',
	           'modelfile=s',
                   'model_origin_id=i',
		   'verbose|v+',
		   'seqs-per-run|N=i',
		  ) || die("$0: you got usage issues, short stack\n");

my $temproot = "/tmp/run-pahmm-$$";
my $seqfn = "$temproot.fa";

my $u = new Unison( dbname=>'csb-dev', username=>'loader' );

(defined $opts{params_id})
  || die("$0: parameters not defined (-p)\n");


$opts{model_origin_id} = 
  $u->selectrow_array(<<EOSQL) if(not defined $opts{model_origin_id});
SELECT model_origin_id FROM params
WHERE params_id=$opts{params_id}
EOSQL
(defined $opts{model_origin_id})
  || die("$0: model_origin_id not defined for params_id=$opts{params_id}\n");

if (not defined $opts{modelfile}) {
  $opts{modelfile} = $u->selectrow_array(<<EOSQL);
SELECT data_url FROM origin
WHERE origin_id=$opts{model_origin_id}
EOSQL
}
(defined $opts{modelfile})
   || die("$0: data_url is NULL for origin_id=$opts{model_origin_id}\n");

(-e $opts{modelfile})
  || die("$0: $opts{modelfile} doesn't exist\n");


my $nmodels = preload_pmodel_id_cache($u,$opts{model_origin_id});

my $clo = $u->run_commandline_by_params_id($opts{params_id});
my @cl = (split(' ',$clo), $opts{modelfile}, $seqfn);


printf(STDERR "# loading params '%s' (params_id=%d)\n",
	   $u->get_params_name_by_params_id($opts{params_id}),
	   $opts{params_id});
printf(STDERR "# %d models from origin '%s' (origin_id=%d)\n",
	   $nmodels,
	   $u->get_origin_name_by_origin_id($opts{model_origin_id}),
	   $opts{model_origin_id});
printf(STDERR "# models in file `%s'\n", $opts{modelfile});
printf(STDERR "# commandline: %s\n", join(' ',@cl));


my @fields = qw(params_id pseq_id start stop pmodel_id mstart mstop gaps qgaps tgaps score eval);
my $pahmm_sth = $u->prepare_cached(sprintf("insert into pahmm (%s) values (%s)",
										   join(',',@fields), join(',',map {'?'} @fields)));


my (@pseq_ids) = @ARGV ? map {eval "$_"} @ARGV : map { chomp;$_ } <>;
my $N = $#pseq_ids+1;
my $n = 0;
my $totseqs = 0;
my $tothits = 0;
my $tothsps = 0;
my $toterrs = 0;
my $totdlock = 0;

while ( @pseq_ids ) {
  my (@ids) = splice(@pseq_ids,0,$opts{'seqs-per-run'});

  my $nseq = fetch_sequences($u,$seqfn,@ids);
  next if ($nseq == 0);

  print(STDERR "fetched $nseq sequences; running...\n");

  my ($seqs,$hits,$hsps,$errs,$dlock) = run_and_load($u,$seqfn);
  $totseqs += $seqs;
  $tothits += $hits;
  $tothsps += $hsps;
  $toterrs += $errs;
  $totdlock += $dlock;

  my $Ncomp = $N - ($#pseq_ids+1);
  printf(STDERR "run-pahmm: $Ncomp/$N (%5.1f%% complete); $totseqs seqs; $tothits hits; $tothsps HSPs; $toterrs errors; $totdlock retries\n",
		 $Ncomp / $N * 100);
}


sub fetch_sequences ($$@) {
  # fetch sequences to temp file
  my $u = shift;
  my $seqfn = shift;
  my @pseq_ids = @_;
  my $nseq = 0;

  my $so = new Bio::SeqIO( -format => 'fasta',
						   -file => ">$seqfn" )
	|| die("! couldn't open $seqfn for writing\n");

  foreach my $pseq_id (@pseq_ids) {
	if ($u->already_ran($pseq_id,$opts{params_id},$opts{model_origin_id},$opts{pmodelset_id})) {
	  warn("Unison:$pseq_id already run with these parameters\n");
	  next;
	}
	my $seq = $u->get_sequence_by_pseq_id( $pseq_id );
	if (not defined $seq) {
	  warn("\n! couldn't find sequence for pseq_id=$pseq_id\n");
	  next;
	}

	if (length($seq) > $opts{'max-length'}) {
	  warn("! Unison:$pseq_id is over $opts{'max-length'} AA; skipping\n");
	  next;
	}

	$so->write_seq( new Bio::PrimarySeq( -seq => $seq,
										 -id => "Unison:$pseq_id" ) );
	$nseq++;
  }
  $so->close();
  return $nseq;
}


sub run_and_load ($$) {
  my $u = shift;
  my $seqfn = shift;
  my $rl_seqs = 0;
  my $rl_hits = 0;
  my $rl_hsps = 0;
  my $rl_errs = 0;
  my $rl_dlock = 0;

  my $hmmerpipe = new IO::Pipe;
  $hmmerpipe->reader( @cl )
	|| die("couldn't do @cl\n");
  my $in = new Bio::SearchIO(-format => 'hmmer',
							 -fh => $hmmerpipe);

  while ( my $result = $in->next_result ) {	# "result" is a query sequence
	my ($pseq_id) = $result->query_name() =~ m/Unison:(\d+)/;
	(defined $pseq_id)
	  || die("couldn't parse pseq_id from hmmer results\n");

	$rl_seqs++;

	my $ndlock = 0;

  RETRY:
	my $nhits = 0;
	my $nhsps = 0;
	try {
	  $u->begin_work();
	  $result->rewind if ($ndlock > 0);
	  while ( my $hit = $result->next_hit ) {
		$nhits++;
		my $acc = $hit->name();
		my $pmodel_id = get_pmodel_id($acc);
		if (not defined $pmodel_id) {
		  die("\n! couldn't look up a pmodel_id for $acc in origin $opts{model_origin_id}!\n");
		}

		$hit->rewind if ($ndlock > 0);
		while ( my $hsp = $hit->next_hsp ) {
		  $nhsps++;
		  my @data = ($opts{params_id}, 
					  $pseq_id, $hsp->start('query'), $hsp->end('query'),
					  $pmodel_id, $hsp->start('hit'), $hsp->end('hit'),
					  $hsp->gaps('total'), $hsp->gaps('query'), $hsp->gaps('hit'),
					  sprintf('%.0f',$hsp->score()), # .0f => round to nearest int
					  ( $hsp->evalue() < $min_double_prec ? 0 : $hsp->evalue() )
					 );
		  $pahmm_sth->execute(@data);
		}
	  }
	  my $z = $u->upd_run_histories($pseq_id,$opts{params_id},
									$opts{model_origin_id},$opts{pmodelset_id});
	  $u->commit();

	  print(STDERR "# Unison:$pseq_id: $nhits hits, $nhsps HSPs; committed $z",
			($ndlock>0 ? "; deadlock resolved after $ndlock dlockies" : ''),
			"\n");
	  $rl_hits += $nhits;
	  $rl_hsps += $nhsps;
	  $rl_dlock += $ndlock;

	} catch Unison::Exception with {

	  my $e = $_[0];
	  if ("$e" =~ m/deadlock detected/) {
		$u->rollback();
		$ndlock++;
		warn("\n$e! Unison:$pseq_id: deadlock $ndlock on HSP $nhsps\n");
		if ($ndlock < 25) {
		  my $sleep = ( $ndlock<5 ? 0 : $ndlock<10 ? 1 : $ndlock<20 ? 10 : 2^($ndlock-15) );
		  sleep $sleep;
		  warn("! Unison:$pseq_id, deadlock $ndlock: resuming after sleeping $sleep seconds.\n");
		  goto RETRY;
		}
		warn("! Too many deadlocks. Aborting Unison:$pseq_id.\n");
	  } elsif ("$e" =~ m/duplicate key/ or "$e" =~ m/(?:over|under)flow/) {
		$u->rollback();
		$rl_errs++;
		warn("\n$e\n! Unison:$pseq_id load failed; rolling back.\n");
	  } else {
		die("$e");
	  }

	};
  }											# end sequence loop

  $hmmerpipe->close();
  return( $rl_seqs, $rl_hits, $rl_hsps, $rl_errs, $rl_dlock );
}




my %pmodel_id_cache;

sub preload_pmodel_id_cache($$) {
  my $u = shift;
  my $origin_id = shift;
  my $ar = $u->selectall_arrayref('select acc,pmodel_id from pmhmm where origin_id=?',
								 undef, $origin_id);
  %pmodel_id_cache = map {$_->[0] => $_->[1]} @$ar;
  return $#$ar+1;
}

sub get_pmodel_id ($) {
  my $acc = shift;
  if (not exists $pmodel_id_cache{$acc}) {
	my $sth = $u->prepare_cached('select pmodel_id from pmhmm where acc=? and origin_id=? order by added desc');
	$sth->execute($acc,$opts{model_origin_id});
	my $ids = $sth->fetchall_arrayref();
	return undef unless defined $ids;
	die("\n! found ",$#$ids+1," pmodel_ids for model acc $acc\n") if ($#$ids>0);
	$pmodel_id_cache{$acc} = $ids->[0]->[0];
  }
  return $pmodel_id_cache{$acc};
}
