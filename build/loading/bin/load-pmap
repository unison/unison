#! /usr/bin/env perl
#-------------------------------------------------------------------------------
# NAME: load-pmap
# PURPOSE: Parse PMAP psl output for protein-to-genome alignments. load
#          output into Unison.
#
# $Id$
#-------------------------------------------------------------------------------

use strict;
use warnings;
use Bio::Tools::ProteinToGenomeBlat;
use Getopt::Long;
use Unison;
use Unison::Exceptions;
use Data::Dumper;

my %opts =
  (
   pct_ident_cutoff => 80,
   score_cutoff => 80,
   verbose => 0,
   params => undef
  );

GetOptions(\%opts,
  'pct_ident_cutoff|i=i',
  'score_cutoff|s=i',
  'help',
  'verbose|v+',
  'params|p=s'
) || usage();

usage() if ($#ARGV != 0 || ! defined $opts{params} );


my $u = new Unison(dbname=>'csb-dev', username=>'loader');
my $aln_sth = $u->prepare( 'select assign_pmap_aln( ?, ?, ?, ?, ?, ?, ? )' );
my $hsp_sth = $u->prepare( 'select ins_pmap_hsp( ?, ?, ?, ?, ?, ?, ? )' );

my $params_id = $u->get_params_id_by_name( $opts{params} );
(defined $params_id)
  || die( "Unable to retrieve params_id for name=$opts{params}" );

my %updated;  # keep track of the pseq_ids that have had their run_history updated


my $bio = new Bio::Tools::ProteinToGenomeBlat( -file => $ARGV[0] );
RESULT:
while( my $r = $bio->next_result() ){
  my @pct_ident  = $r->get_tag_values('percent_id');
  my @ident      = $r->get_tag_values('ident');
  my @qgap_cnt   = $r->get_tag_values('qgap_cnt');
  my @qgap_bases = $r->get_tag_values('qgap_bases');
  my @tgap_cnt   = $r->get_tag_values('tgap_cnt');
  my @tgap_bases = $r->get_tag_values('tgap_bases');
  my $pseq_id;

  if ( not defined $r->seq_id ) {
	warn( "ERROR: line $.: result seq_id is undefined; skipping.\n",
		  $_, "\n" );
	next RESULT;
  }
  if ( not $r->seq_id =~ m/Unison:(\d+)/ ) {
    die( "ERROR: can't parse pseq_id from seq_id=",$r->seq_id,"\n",
		 "Source sequences aren't from Unison!\n");
  }
  $pseq_id = $1;


  # insertion criteria: score >= score_cutoff and pct_ident >= pct_ident_cutoff
  if ( not ($r->score >= $opts{score_cutoff} 
			and $pct_ident[0] >= $opts{pct_ident_cutoff} ) ) {
	if ( $opts{verbose} ) {
	  printf(STDERR "Unison:%-10d PMAP alignment failed cutoffs (ident: %5d (%d), score: %4.2f (%4.2f))\n",
			 $pseq_id,$pct_ident[0],$opts{pct_ident_cutoff},$r->score,$opts{score_cutoff});
	}
	next RESULT;
  }

 RETRY:
  try {
    $u->begin_work();

    my (@hsp_ids,$chr);
    foreach my $f ( $r->get_SeqFeatures() ) {
      if ( not defined $f->seq_id ) {
	warn( "ERROR: line $.: feature seq_id is undefined; skipping.\n",
	      $_, "\n" );
	next RESULT;
      }
      if ( not $f->seq_id =~ m/chr(\w+)/ ) {
	warn( "ERROR: line $.: can't parse chromosome from seq_id=(",$f->seq_id,"); skipping.\n" );
	next RESULT;
      }
      $chr = $1;

      # load each HSP

      $hsp_sth->execute( $chr,(( $f->strand > 0 ) ? 'true' : 'false'),$f->start, $f->end, $pseq_id, $f->hstart, $f->hend );
      push @hsp_ids,$hsp_sth->fetchrow_array;

    }

    # get serialized hsp_str - probably should use database version
    my $hsp_str = join (':', @hsp_ids );

    # load the alignment
    if ( $opts{verbose} ) {
      printf(STDERR "Unison:%-10d mapped to chr%2s (ident: %5d, score: %4.2f, exons: %3d)\n",
	     $pseq_id,$chr,$ident[0],$r->score,($#hsp_ids+1));
    }

    $aln_sth->execute( $hsp_str, $ident[0], $qgap_cnt[0], $qgap_bases[0],
		       $tgap_cnt[0], $tgap_bases[0], $params_id );

    # update run history if we haven't yet done so during this session
    $u->upd_run_history($pseq_id,$params_id,undef,undef) unless $updated{$pseq_id}++;
    $u->commit();
  } catch Unison::Exception with {
    my $e = $_[0];
    if ("$e" =~ m/deadlock detected/) {
      $u->rollback();
      warn("$e; retrying.\n");
      goto RETRY;
    }
    die("$e") unless $e->error() =~ m/unique constraint/;
  };

}



#-------------------------------------------------------------------------------
# NAME: usage
# PURPOSE: output usage message and then exit
#-------------------------------------------------------------------------------
sub usage {
  die( "USAGE: load-pmap [OPTIONS] output.psl\n" ,
    "\tOPTIONS:\n",
    "\t\t-h|--help: output USAGE statement and exit\n",
    "\t\t-p <params> | --params=<name>: load pmap alignments generated with these parameters\n",
    "\t\t-i <cutoff> | --pct_ident_cutoff=<cutoff>: only accept HSPs with percent identity greater than cutoff\n",
    "\t\t-s <cutoff> | --score_cutoff=<cutoff>: only accept HSPs with score greater than cutoff\n" );
}
