#!/usr/bin/env perl
# $Id: load-pmhmm,v 1.3 2003/07/10 20:53:39 rkh Exp $
# load pfam HMM summary info (not the HMM itself) into unison
# $ load-pmhmm <Pfam_fs

use strict;
use warnings;
use Unison;
use Getopt::Long qw(:config gnu_getopt);


my %opts;
GetOptions( \%opts,
			'origin|o=s' )
  || die("Sarge, you've got usage problems; see code\n");


if (not exists $opts{origin}) {
  die("$0: missing required --origin option\n");
}

my $u = new Unison;
my $porigin_id = $u->porigin_si_porigin_id($opts{origin});
printf(STDERR "# loading sequences into $opts{origin} (porigin_id=$porigin_id)\n");

my $sth_fetch_by_acc = $u->prepare_cached('select pmodel_id,cksum from pmhmm where acc=?');
my $sth_update_origin = $u->prepare("update pmhmm set porigin_id=? where pmodel_id=?");
my $sth_insert_pmhmm = $u->prepare("insert into pmhmm (porigin_id,name,acc,descr,len,nseq,cksum) ".
						"values ($porigin_id,?,?,?,?,?,?)");


$/ = "\nHMMER2.0";
while(my $blk = <>) {
  my ($name,$acc,$descr,$len,$nseq,$cksum) 
	= map {$blk =~ m/^$_\s+(.+)/m}  qw(NAME ACC DESC LENG NSEQ CKSUM);

  # cases:
  # given a new Pfam acc and cksum (from file being read):
  # 1) model acc in db?  Yes:
  #   a) cksums equal? Yes:
  #     update origin
  #   b) else:
  #     insert as new
  # 2) model acc not in db?
  #   insert
  # 3) model acc in db, not in 
  #   nothing (we wouldn't know about it solely from input file)

  my (@extant) = extant_models_by_acc($u,$acc);

  # it's almost inconceivable that there would be more than one model
  # with a matching checksum and same name, but we'll play it safe...
  my (@update_ids) = map {$_->[0]} grep {$_->[1]==$cksum} @extant;

  if (@update_ids) {
	printf(STDERR "updating %d ids (%s) for $acc ($name)\n",
		   $#update_ids+1,join(',',@update_ids) );
	$sth_update_origin->execute($porigin_id,$_) for @update_ids;
  } else {
	printf(STDERR "inserting $acc ($name)\n");
	$sth_insert_pmhmm->execute($name,$acc,$descr,$len,$nseq,$cksum);
  }
}

printf("done; last updated=%s\n", $u->porigin_last_updated($porigin_id,1));


sub extant_models_by_acc {
  my ($u,$acc) = @_;
  $sth_fetch_by_acc->execute($acc);
  my $rv = $sth_fetch_by_acc->fetchall_arrayref();
  $sth_fetch_by_acc->finish();
  return(@$rv);
}
