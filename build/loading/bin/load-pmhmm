#!/usr/bin/env perl
# $Id: load-pmhmm,v 1.9 2005/04/11 18:29:36 rkh Exp $
# load pfam HMM into unison
# eg$ load-pmhmm /path/to/pfam-14.0/Pfam_fs


use strict;
use warnings;
use Unison;
use Getopt::Long qw(:config gnu_getopt);
use Data::Dumper;
use IO::Pipe;


my $fn;
($fn = shift and $fn =~ m%^/%)
  || die("$0: please privide the full path to the Pfam models\n");

my $in;
if ($fn =~ m/.gz/) {
  $in = new IO::Pipe;
  $in->reader( 'gzip', '-cdq', $fn );
} else {
  die("$0: I expected the gzip-compressed HMM file\n");
}

my $hmmb_path = $fn;
$hmmb_path =~ s/.gz$//;
$hmmb_path .= '.hmmb';
(-e $hmmb_path)
  || die("$0: You must make `$hmmb_path' before proceeding\n");

my ($pfam_version,$fl) = $fn =~ m%pfam-([\.\d]+)/Pfam_([lf]s)%;
(defined $fl)
  || die("$0: couldn't parse origin (pfam-<version>/Pfam_([fl]s) from $fn\n");

my $origin = "Pfam $pfam_version $fl";

my $u = new Unison( dbname=>'csb-dev', username=>'loader' );

my $porigin_id = $u->porigin_si_porigin_id($origin);
(defined $porigin_id)
  || die("$0: origin $origin: $@\n");
my $sth = $u->prepare('update porigin set url=?,descr=?,data_url=? where porigin_id=?');
$sth->execute('http://pfam.wustl.edu/cgi-bin/getdesc?acc=%s',
			  "Pfam_$fl version $pfam_version",
			  $hmmb_path,
			  $porigin_id)
  || die("$0: update porigin: $@\n");
printf(STDERR "# loading models into $origin (porigin_id=$porigin_id)\n");



my @insert_fields = qw(porigin_id name acc descr len nseq cksum
					ga_seq ga_dom tc_seq tc_dom nc_seq nc_dom);
my $insert_stmt = sprintf('insert into pmhmm (%s) values (%s)',
						  join(',',@insert_fields),
						  join(',',map {'?'} @insert_fields) );
my $sth_insert_pmhmm = $u->prepare($insert_stmt);

warn($insert_stmt);


$/ = "\n//\n";
$u->begin_work();
while(my $blk = <$in>) {
  chomp($blk);
  my ($name,$acc,$descr,$len,$nseq,$cksum,$ga,$tc,$nc) 
	= map {$blk =~ m/^$_\s+(.+)/m}  qw(NAME ACC DESC LENG NSEQ CKSUM GA TC NC);
  my ($gas,$gad) = split(' ',$ga);
  my ($tcs,$tcd) = split(' ',$tc);
  my ($ncs,$ncd) = split(' ',$nc);
  $sth_insert_pmhmm->execute($porigin_id,$name,$acc,$descr,$len,$nseq,$cksum,
							 $gas,$gad, $tcs,$tcd, $ncs,$ncd);

  print(STDERR "loaded $acc: $name ($descr)\n");
}

printf("done; last updated=%s\n", $u->porigin_last_updated($porigin_id,1));

print("Committing...\n");
$u->commit();
