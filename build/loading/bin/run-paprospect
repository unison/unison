#!/usr/bin/env perl
# run p2 and insert into Unison
#-------------------------------------------------------------------------------
# name: run-p2
# purpose: run prospect locally and insert the results into Unison
# arguments: 
#   --p2params_id|-p <p2params_id>: p2params_id for loading (required)
#   --tfile|-t <template file>: file containing a list of templates (optional)
#   --update: force update (i.e. delete cooresponding rows in unison before inserting) (optional)
# usage: run-p2 [psql options] --p2params_id <p2params_id> [--tfile <template file>] [--update]
#
# $Id: run-p2,v 1.8 2003/10/28 22:29:05 rkh Exp $
#-------------------------------------------------------------------------------

use warnings;
use strict;

select STDERR; $|++;
select STDOUT; $|++;

use Unison;
use Bio::Prospect::Options;
use Bio::Prospect::LocalClient;
use Bio::Prospect::Exceptions;
use Getopt::Long qw(:config gnu_getopt);
use Data::Dumper;
use Error qw(:try);


my %opts = (
			update =>0,
			templates => [] );
GetOptions(\%opts,
		   'p2params_id|p=i',
		   'tfile|T=s',						# name of template file
#		   'templates|t=s' => \@{$opts{templates}},
		   'update'							# delete rows before insert
		  )
  or die("$0: bad usage\n");

if ( not exists $opts{p2params_id} or $opts{p2params_id} eq '' ) {
  die( "--p2params_id|-p parameter missing\n" ); }


my $u = new Unison;

my $po = $u->get_p2options_by_params_id($opts{p2params_id});
$po->{tfile} = $opts{tfile}
  || '/apps/compbio/share/prospect2/tfiles/all';
my $pc = new Bio::Prospect::LocalClient( options=>$po );


PSEQ_ID: while( my $pseq_id = shift ) {
  my $seq = $u->get_sequence_by_pseq_id( $pseq_id );
  my (@summaries);
  try {
	printf("threading $pseq_id (%d AA)...", length($seq));
    (@summaries) = $pc->thread_summary( $seq );
  } catch Bio::Prospect::RuntimeError with {
    my $e = shift;
    print STDERR "Caught Bio::Prospect::RuntimeError: " . $e->detail() . "\n";
    print STDERR "Skipping this pseq_id ($pseq_id)\n";
    next PSEQ_ID;
  } catch Bio::Prospect::Exception with {
    my $e = shift;
    print STDERR "Caught Bio::Prospect::Exception: " . $e->detail() . "\n";
    print STDERR "Skipping this pseq_id ($pseq_id)\n";
    next PSEQ_ID;
  };
  print "\ndone";
  for( my $i=0; $i<=$#summaries; $i++) {
	my $ts = $summaries[$i];
	printf(STDERR "\rloading %8s (%4d/%4d %3d%%)...", 
		   $ts->tname(), ($i+1), ($#summaries+1), ($i+1)/($#summaries+1)*100 );
    $u->delete_thread($pseq_id,$opts{p2params_id},$ts) if $opts{update};
    $u->insert_thread($pseq_id,$opts{p2params_id},$ts);
  }
  print(STDERR "done\n");
}


############################################################################
# $Log: run-p2,v $
# Revision 1.8  2003/10/28 22:29:05  rkh
# * new Unison doesn't require special handling for PGPASSWORD (see U::DBI)
#
# Revision 1.6  2003/06/19 00:14:36  rkh
# * reworked options to use hashes
# * set $po->{tfile} only when $opt{tfile} is set
# * pretty print progress
#
