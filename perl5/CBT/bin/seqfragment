#!/usr/bin/env perl

=head1 NAME

seqfragment -- fragment sequences from a fasta file
S<$Id: seqfragment,v 1.1 2003/05/05 20:14:04 rkh Exp $>

=head1 SYNOPSIS

seqfragment [opts] <in.fasta >out.fasta

=head1 DESCRIPTION

B<seqfragment> fragment sequences from a fasta file into multiple
subsequences of specified length and overlap (or offset).

=head1 OPTIONS AND ARGUMENTS

=over

=item B<-l> n, B<--length> n

fragment sequences into subsequences of length n (n>0)

=item B<-v> n, B<--overlap> n

overlap subsequences by n residues (default = 0)

=item B<-o> n, B<--offset> n

offset subsequences by n residues (instead of specifying overlap).  If
offset is greater than length, skip gaps will be created between
subsequences.

=item B<-z>, B<--zero>

begin residue numbering at zero (default is to start at 1)

=item B<-i> n, B<--id-format> n

use one of the following id tag formats for fasta sequence identifiers:

	1   id -> id[b:e] /fragment=<n>/<N> /len=<len>
	2   id -> idN /coordinates=[b:e] /len=<len>

=item B<-V>, B<--constant-overlap>

=back

=cut



use strict;
use warnings;
use Getopt::Long qw(:config gnu_getopt);
use Bio::SeqIO;
use Bio::Seq;
use Pod::Usage;

my %options =
  (
   zero => 0,
   length => undef,  # not implemented: 'constant-length' => 1,
   overlap => 0,  'constant-overlap' => 0,
   offset => undef,
   'id-format' => 1,
   debug => 0,
   'start-fragment' => 1,
   'end-fragment' => undef,
   );
GetOptions(
		   \%options,
		   'length|l=i', # not implemented: 'constant-length|L',
		   'overlap|v=i', 'constant-overlap|V',
		   'offset|o=i',
		   'start-fragment|f=i',
		   'end-fragment|F=i',
		   'id-format|i=i',
		   'zero|z',
		   'debug|d+',
		   'help|h',
		  )
  || die("$0: bad usage; try --help\n");

exists $options{help}
  && pod2usage( { -verbose => 1 } );

# sanity checks
(defined $options{length})
  || die("$0: length not defined\n");
($options{length} > 0)
  || die("$0: length must be > 0\n");
(defined $options{overlap}
 and $options{overlap} >= 0)
  || die("$0: overlap must be >= 0\n");
($options{overlap} < $options{length})
  || die("$0: overlap must be less than sequence length\n");
if (not defined $options{offset})
  { $options{offset} = $options{length} - $options{overlap}; }
(defined $options{offset}
 and $options{offset} > 0)
  || die("$0: offset must be > 0\n");


# setup
my $si = new Bio::SeqIO( -format => 'Fasta' );
my $so = new Bio::SeqIO( -format => 'Fasta' );
my $offset = $options{offset};
my $len = $options{length};
$options{zero} = 1-$options{zero};
if ($options{debug})
  {
  printf(STDERR "# length=%d, overlap=%d, offset=%d\n",
		 @{%options}{qw(length overlap offset)})
  }

# process sequences
while( my $s0 = $si->next_seq() )
  {
  my $seq = $s0->seq();
  my $l0 = length($seq);
  my $id = $s0->display_id();
  my $e = 0;

  if ($options{debug})
	{
	printf(STDERR "%2s %-30.30s %3s:%3s %3s %s\n",
		   '','','b','e','l',('0    5    'x9));
	printf(STDERR     "%2s %30s %3d:%3d %3d %s\n", 
		   'i', 'in ->', 0, $l0-1, $l0, $seq);
	}

  for( my $i=$options{'start-fragment'}-1;
	   (defined $options{'end-fragment'} ? $i<=$options{'end-fragment'}-1 : 1)
	     and $e<$l0-1;
	   $i++ )
	{
	my $b = $i * $offset;
	$e = $b + $len - 1;
	if ($e > $l0-1)
	  {
	  if ($options{'constant-overlap'})
		{ $e = $l0-1; }
	  else
		{ $b = $l0-$len > 0 ? $l0-$len : 0;  $e = $l0-1; }
	  }
	my $l = $e-$b+1;
	my $s = new Bio::Seq( -seq => substr($seq,$b,$l) );
	annotate_seq($s,\%options,$id,$i+1,$b,$e,$l);
	if ($options{debug})
	  {
	  printf(STDERR "%2d %-30.30s %3d:%3d %3d %s\n",
			 $i+1, $s->display_id() . ' (' . $s->desc() . ')',
			 $b,$e,$l,' 'x$b . $s->seq());
	  }
	$so->write_seq($s);
	}
  }

exit(0);



sub annotate_seq
  {
  my ($s,$options,$id,$i,$b,$e,$l) = @_;
  $b += $options{zero};
  $e += $options{zero};
  if ($options{'id-format'} == 1)
	{ $s->display_id("$id\[$b:$e]"); }
  elsif ($options{'id-format'} == 2)
	{ $s->display_id("${id}_$i"); }
  $s->desc("/coords=[$b:$e] /fragment=$i /len=$l");
  return $s;
  }


=pod

=head1 BUGS

I'm (like so) sure.

=head1 SEE ALSO

=head1 AUTHOR

 Reece Hart, Ph.D.                     rkh@gene.com, http://www.gene.com/
 Genentech, Inc.                       650/225-6133 (voice), -5389 (fax)
 Bioinformatics Department             
 1 DNA Way, MS-93                      http://www.in-machina.com/~reece/
 South San Francisco, CA  94080-4990   reece@in-machina.com, GPG: 0x25EC91A0

=cut

