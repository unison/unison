#!/usr/bin/env perl
# get-seq -- output selected sequences from Unison in fasta format
# $Id: get-seq,v 1.8 2003/10/28 21:51:28 rkh Exp $

use warnings;
use strict;
use Unison;
use Getopt::Long qw(:config gnu_getopt);


my %opts = 
  (
   'best-alias' => 0,
   'best-annotation' => 0,
   'all-aliases' => 0,
   'proid' => [],
   'verbose' => 0,
  );
GetOptions( \%opts,
			'best-alias|best-oa|b+',
			'best-annotation|best-oad|B+',
			'all-aliases|a+',
			'proid|P=s@',
			'verbose|v+'
		  )
  || die("$0: you got usage issues, homey\n");

my $u = new Unison();
die( "! couldn't open a connection to unison\n" ) unless $u;

@{$opts{proid}} = map {split(/,/,$_)} @{$opts{proid}};
unshift(@ARGV, SPDI_lookup('PRO',@{$opts{proid}}));

if (@ARGV) {
  while( my $r = shift ) {
	process1($_) for eval "$r";
  }
} else {
  while (my $id=<>)	{
	chomp $id;
	process1($id) }
}

exit(0);

############################################################################

sub process1 {
  my $pseq_id = shift;
  my $seq = get1($pseq_id);
  if (not defined $seq) {
	warn("Unison:$pseq_id not found\n"); return;
  }
  print $seq;
}

sub get1 {
  my $pseq_id = shift;
  my @descr;
  my $rv;
  my $seq = $u->get_sequence_by_pseq_id($pseq_id);

  if (not defined $seq)
	{ warn("! pseq $pseq_id doesn't exist\n"); return; }

  if ($opts{'best-alias'})
	{ (@descr) = $u->best_alias($pseq_id,1) }
  elsif ($opts{'best-annotation'})
	{ (@descr) = $u->best_annotation($pseq_id,1) }
  elsif ($opts{'all-aliases'})
	{ (@descr) = $u->pseq_get_aliases($pseq_id) }

  @descr = () unless @descr;

  $rv = join(' ',">Unison:$pseq_id",@descr) . "\n";
  while( length($seq) )
	{ $rv .= substr($seq,0,60,'') . "\n"; }

  print(STDERR "# $pseq_id (", $descr[0]||'<no description>', ")\n") if $opts{verbose};
  return $rv;
}



sub SPDI_lookup {
  my $pfx = shift;
  return map {SPDI_lookup1($pfx,$_)} @_;
}

sub SPDI_lookup1 {
  my ($pfx,$id) = @_;
  my $sth = $u->prepare_cached(q/select pseq_id from palias where alias=? and
 								porigin_id=porigin_id_lookup('SPDI')/);
  $sth->execute("$pfx$id");
  my $rv = $sth->fetchrow_array;
  $sth->finish();
  return $rv;
}





=pod

=head1 NAME

get-seq -- retrieve sequences from Unison

S<$Id: get-seq,v 1.8 2003/10/28 21:51:28 rkh Exp $>

=head1 SYNOPSIS

 get-seq [-a|-A|-D] [pseq_id ...]

=head1 OPTIONS

=item B<--alpha> level, B<--Arpha> level, B<-a> level

=head1 DESCRIPTION

B<get-seq> writes selected sequences from Unison in fasta format to
STDOUT, optionally with aliases or annotations.

=cut
